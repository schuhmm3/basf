plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id("maven-publish")
    id 'jacoco'
}


group = 'com.basf'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jar {
    enabled = true
}

bootJar {
    archiveClassifier = 'executable'
}

jacoco {
    toolVersion = "0.8.6"
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

ext {
    set('springCloudVersion', "2020.0.0")
}

bootRun {
    systemProperties = System.properties
}


dependencies {
    implementation group: 'org.apache.tika', name: 'tika-parsers', version: '1.25'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-webflux-ui', version: '1.5.2'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation group: 'com.googlecode.jmapper-framework', name: 'jmapper-core', version: '1.6.1.CR2'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
    testImplementation 'io.projectreactor:reactor-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
    dependsOn test
}

task unitTest( type: Test ) {
    maxHeapSize = "1g"
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }
    exclude '**/*IntegrationTest*'
    finalizedBy jacocoTestReport
}

task intTest( type: Test ) {
    maxHeapSize = "1g"
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardError"
    }
    include '**/*IntegrationTest*'
    finalizedBy jacocoTestReport
}

test {
    onlyIf { !project.hasProperty("skipTests") }
    useJUnitPlatform()
}
